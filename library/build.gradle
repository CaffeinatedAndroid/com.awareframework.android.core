apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'

project.ext {
    saferoom = System.getenv("saferoom") ?: "0.4.2"
    room = System.getenv("room") ?: "1.1.0"
    fuel = System.getenv("fuel") ?: "1.12.1"
    gson = System.getenv("gson") ?: "2.8.2"
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Support Dependencies
    implementation "com.android.support:appcompat-v7:$support_libs"

    // ROOM
    implementation "android.arch.persistence.room:runtime:$room"
    kapt "android.arch.persistence.room:compiler:$room"
    implementation "com.commonsware.cwac:saferoom:$saferoom"

    implementation "com.google.code.gson:gson:$gson"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

android {

    compileSdkVersion project.ext.compile_sdk
    buildToolsVersion project.ext.build_tools

    defaultConfig {
        minSdkVersion project.ext.minimum_sdk
        targetSdkVersion project.ext.target_sdk
        versionCode project.ext.version_code
        versionName project.ext.version_readable

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}